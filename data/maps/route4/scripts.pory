mapscripts route4_MapScripts {}

script route4_walkDown_left{
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, route4_playerWalkDown_left)
    goto(route4_stellaLookAround)
}
script route4_walkDown_middle{
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, route4_playerWalkDown_middle)
    goto(route4_stellaLookAround)
}
script route4_walkDown_right{
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, route4_playerWalkDown_right)
    goto(route4_stellaLookAround)
}

//stella script broken up into parts for easy work
//intro and her going up to you
script route4_stellaLookAround{
    waitmovement(OBJ_EVENT_ID_PLAYER)
    applymovement(2, route4_stellaLooking)
    waitmovement(2)
    msgbox("I wonder where {PLAYER} is…", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(2, route4_stellaLooking)
    waitmovement(2)
    applymovement(2, route4_stellaSurprise)
    waitmovement(2)
    applymovement(2, route4_stellaWalkUp)
    waitmovement(2)
    applymovement(OBJ_EVENT_ID_PLAYER, route4_playerLookRight)
    msgbox("THERE you are!\nI was starting to get worried!\pSoooo, I heard about your battle with\nJohnny!\lI bet you CRUSHED him, haha!\pOh!\nHey!\pWhy don't we have a battle?!", MSGBOX_AUTOCLOSE)
    goto(route4_starterBattleIntro)
}
//The Shape approaches
script route4_starterBattleIntro{
    waitmessage
    special(HealPlayerParty)
    msgbox("Here, I'll even heal your party so th-", MSGBOX_AUTOCLOSE)
    waitmessage
    setvar(VAR_RUSTBORO_CITY_STATE, 3)
    setflag(FLAG_DISABLE_RUNNING_AWAY)
    applymovement(1, route4_starterWalkUp)
    fadeoutbgm(1)
    waitmovement(1)
    applymovement(2, route4_stellaLookDownShocked)
    applymovement(OBJ_EVENT_ID_PLAYER, route4_stellaLookDownShocked)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox("U-u-umm…\n{PLAYER}…?", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(1, route4_starterWalkUp)
    waitmovement(1)
    applymovement(1, route4_starterWalkLeft)
    waitmovement(1)
    applymovement(1, route_4_starterWalkRight)
    waitmovement(1)
    applymovement(1, route4_starterWalkLeft)
    waitmovement(1)
    msgbox("Wh… wh-what i-i-is THAT!?!", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(1, route4_starterAttack)
    waitmovement(1)
    goto(route4_starterBattleCommence)
}
//starter battle
script route4_starterBattleCommence{

    switch(var(VAR_STARTER_MON))
    {
        case(0): call(route4_evoStarterBattle_croconut)
        case(1): call(route4_evoStarterBattle_skrappup)
        case(2): call(route4_evoStarterBattle_kitfisk)
    }
    specialvar(VAR_RESULT, GetBattleOutcome)
    if(var(VAR_RESULT) == B_OUTCOME_WON)
    {
        goto(route4_endCutscene)
    }
    elif(var(VAR_RESULT) == B_OUTCOME_PLAYER_TELEPORTED)
    {
        goto(route4_koFailsafe)
    }
}
//after the starter battle
script route4_endCutscene{
    setvar(VAR_RUSTBORO_CITY_STATE, 4)
    clearflag(FLAG_DISABLE_RUNNING_AWAY)
    setobjectxy(1, 16, 7)
    setobjectxy(3, 17, 11)
    setobjectxy(2, 17, 6)
    applymovement(3, route4_scientist_walkUp)
    waitmovement(3)
    msgbox("Ah, apologies.\pThis one must've gotten out.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(2, route4_stellaShuffle)
    msgbox("U-uh-\pH-huh?! What!?", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(3, route4_scientistShuffle)
    msgbox("I'll be taking this one back to the\nlab.\pAgain, my sincerest apologies.\nThings like this don't usually happen.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(3, route4_scientistShuffle)
    playfanfare(MUS_OBTAIN_ITEM)
    removeobject(1)
    waitmovement(3)
    applymovement(3, route4_scientistExit)
    waitmovement(3)
    removeobject(3)
    applymovement(2, route4_stellaFaceLeft)
    applymovement(OBJ_EVENT_ID_PLAYER, route4_playerLookRight)
    msgbox("I… u-um…\p… … …\p…I'm… g-going to… go t-tell Dad.\nA-about this.\lI-I guess…\pI, u-uh…\p… … …\pS-see you b-back at th-the Lab, then…", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(2, route4_scientistExit)
    waitmovement(2)
    removeobject(2)
    setflag(FLAG_UNUSED_0x021)
    releaseall
}
//setting things in case of ko
script route4_koFailsafe{
    setvar(VAR_RUSTBORO_CITY_STATE, 3)
    clearflag(FLAG_DISABLE_RUNNING_AWAY)
    releaseall
}
//post-ko rematch
script route4_postKo_right{
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, route4_playerWalkDown_right)
    goto(route4_postKoRematch)
}
script route4_postKo_left{
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, route4_playerWalkDown_left)
    goto(route4_postKoRematch)
}
script route4_postKo_middle{
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, route4_playerWalkDown_middle)
    goto(route4_postKoRematch)
}
script route4_postKoRematch{
    setobjectxy(1, 16, 7)
    setobjectxy(2, 17, 6)
    setflag(FLAG_DISABLE_RUNNING_AWAY)
    applymovement(1, route4_starterAttack)
    waitmovement(1)
    goto(route4_starterBattleCommence)
}

script route4_evoStarterBattle_croconut{
    setwildbattle(SPECIES_SANDILE, 10)
    dowildbattle
}
script route4_evoStarterBattle_skrappup{
    setwildbattle(SPECIES_FENNEKIN, 10)
    dowildbattle
}
script route4_evoStarterBattle_kitfisk{
    setwildbattle(SPECIES_LITTEN, 10)
    dowildbattle
}

//stella looking around
movement route4_stellaLooking{
    walk_in_place_left*2
    walk_in_place_right*2
}
//stella seeing you
movement route4_stellaSurprise{
    walk_in_place_up*2
    emote_exclamation_mark
}
//you facing stella
movement route4_playerLookRight{
    face_right
}
//stella walking up and facing you
movement route4_stellaWalkUp{
    walk_up*2
    face_left
}
//stella looking at The Shape
movement route4_stellaLookDownShocked{
    emote_exclamation_mark
    face_down
}

//The Shape walking up
movement route4_starterWalkUp{
    walk_up
}
//The Shape going left and looking up at you two
movement route4_starterWalkLeft{
    walk_left
    face_up
    delay_16
}
//The Shape going right and looking up at you two
movement route_4_starterWalkRight{
    walk_right
    face_up
    delay_16
}
//The Shape coming in for the kill
movement route4_starterAttack{
    walk_in_place_up*3
    delay_4
    walk_in_place_fast_up*3
    walk_fast_up*2
}

//you coming down from the left trigger
movement route4_playerWalkDown_left{
    walk_down*3
    walk_right
    face_down
}
//you coming down from the middle trigger
movement route4_playerWalkDown_middle{
    walk_down*3
}
//you coming down from the right trigger
movement route4_playerWalkDown_right{
    walk_down*3
    walk_left
    face_down
}

//scientist walk up
movement route4_scientist_walkUp{
    walk_up*3
    walk_left
    face_up
}
//stella shuffle in place
movement route4_stellaShuffle{
    walk_in_place_down*2
}
//scientist shuffle in place
movement route4_scientistShuffle{
    walk_in_place_up*2
}
//scientist exit
movement route4_scientistExit{
    walk_right
    walk_down*6
}
//stella look left
movement route4_stellaFaceLeft{
    face_left
}